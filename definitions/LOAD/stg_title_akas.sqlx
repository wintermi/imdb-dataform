config {
  hasOutput: true,
  type: "operations",
  schema: dataform.projectConfig.vars.RAW_DATA,
  tags: ["LOAD_DATA"]
}


CREATE OR REPLACE TABLE ${ self() }
(
    title_id STRING NOT NULL OPTIONS(description="A tconst, an alphanumeric unique identifier of the title.")
  , ordering INT64 OPTIONS(description="A number to uniquely identify rows for a given title_id.")
  , title STRING OPTIONS(description="The localized title.")
  , region STRING OPTIONS(description="The region for this version of the title.")
  , language STRING OPTIONS(description="The language of the title.")
  , types STRING OPTIONS(description="Enumerated set of attributes for this alternative title. One or more of the following: 'alternative', 'dvd', 'festival', 'tv', 'video', 'working', 'original', 'imdbDisplay'. New values may be added in the future without warning.")
  , attributes STRING OPTIONS(description="Additional terms to describe this alternative title, not enumerated")
  , is_original_title BOOL OPTIONS(description="False: not original title; True: original title.")
);


LOAD DATA INTO ${ self() }
FROM FILES (
  format = "CSV",
  compression = "GZIP",
  field_delimiter = "\t",
  skip_leading_rows = 1,
  allow_jagged_rows = true,
  quote = "",
  null_marker = "\\N",
  uris = ["gs://${ dataform.projectConfig.vars.LOAD_GCS_BUCKET }/title.akas.tsv.gz"]
);

